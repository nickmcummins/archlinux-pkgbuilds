# Maintainer: Samuel Mesa <samuelmesa@linuxmail.org>
# Contributor: F0ruD<fzerorubigd{AT}gmail{DOT}com>
# Contributor: Nick M Cummins <nickmcummins at linuxmail dog org>
# Built: 2020-06-17 09:25PM PT

pkgname=libsquish-svn
pkgver=1.15.r110
pkgrel=1
pkgdesc="DXT compression library - svn build"
arch=('i686' 'x86_64')
url="http://libsquish.sourceforge.net"
makedepends=('subversion' 'cmake')
license=('MIT')
provides=('libsquish')
conflicts=('libsquish')
_svnurl=http://svn.code.sf.net/p/libsquish/code
source=("${pkgname}::svn+${_svnurl}/trunk")
md5sums=('SKIP')

pkgver() {
  SVNTAGS=$(svn log -v ${_svnurl}/tags/ | awk '/^   A/ { print $2 }')
  SVNTAG=$(python -c "import sys; svntags = sys.argv[1]; svntags = list(set(map(lambda tag: tag.replace('/tags/', ''), filter(lambda line: line.startswith('/tags/'), svntags.split('\n'))))); svntags.sort(); print(svntags[-1])" ${SVNTAGS})
  SVNREVISION=$(svnversion "${SRCDEST}/${pkgname}" | tr -d [A-z])
  printf "${SVNTAG}.r${SVNREVISION}"
}

build() {
  cd "${pkgname}"  

  #Build
  if [[ -d "build" ]]; then
    (rm -rf build)
  fi

  mkdir build
  cd build

  cmake .. \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DCMAKE_BUILD_TYPE=Release \
  -DBUILD_SHARED_LIBS=ON \
  -DBUILD_SQUISH_EXTRA=ON \
  -DBUILD_SQUISH_WITH_OPENMP=ON \
  -DBUILD_SQUISH_WITH_SSE2=ON
  make
}

package() {
  cd ${pkgname}/build
  
  cmake -D CMAKE_INSTALL_PREFIX="$pkgdir/usr" -P cmake_install.cmake

  install -Dm 644 ../CMakeModules/FindlibSquish.cmake "$pkgdir/usr/share/cmake/Modules/FindlibSquish.cmake"
  install -Dm 644 ../LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
