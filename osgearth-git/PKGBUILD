# Maintainer: Nick M Cummins

pkgname=osgearth-git
_gitname=osgearth
pkgver=3.7.1.r15430.6fa787cff
pkgrel=1
pkgdesc="A terrain rendering toolkit for OpenSceneGraph"
arch=('i686' 'x86_64')
url='http://www.osgearth.org'
license=('LGPL')
depends=('openscenegraph' 'gdal' 'expat' 'curl' 'gdal' 'rocksdb' 'leveldb' 'blend2d' 'protobuf' 'arrow' 'netcdf' 'podofo' 'cfitsio')
optdepends=('geos: Improved vector support'
            'sqlite: Flat file cache'
            'libzip: Archive support'
            'blend2d: Blend2D support')
makedepends=('git' 'cmake')
source=("$_gitname::git+https://github.com/gwaldron/osgearth")
provides=('osgearth')
conflicts=('osgearth-qt4' 'osgearth')
md5sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_gitname}"
    VERSION=$(python -c "import sys; import re; cmake_filename = 'CMakeLists.txt'; cmake_file = open(cmake_filename, 'r'); cmake_vars_regex = r'(set\s*\()([^\s]+)\s*([^)]+)'; vars = dict(list(map(lambda varline: (re.search(cmake_vars_regex, varline).group(2), re.search(cmake_vars_regex, varline).group(3)), filter(lambda line: line.startswith('set('), cmake_file.read().split('\n'))))); major_version = vars['OSGEARTH_MAJOR_VERSION']; minor_version = vars['OSGEARTH_MINOR_VERSION']; build_version = vars['OSGEARTH_PATCH_VERSION']; version = f'{major_version}.{minor_version}.{build_version}'; print(version)")
    printf "${VERSION}.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir/$_gitname"

    mkdir -p build && cd build
    cmake .. \
    		-DLIB_POSTFIX= \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DOpenGL_GL_PREFERENCE=GLVND \
        -DOSGEARTH_BUILD_LEGACY_MP_TERRAIN_ENGINE=OFF \
        -DOSGEARTH_INSTALL_SHADERS=ON \
        -DOSGEARTH_ENABLE_NVTT_CPU_MIPMAPS=ON \
         -DROCKSDB_LIBRARY=/usr/lib/librocksdb.so
    make
}

package() {
   cd "$srcdir/$_gitname"/build
   make DESTDIR="$pkgdir" install
   mkdir -p "$pkgdir"/usr/share/osgearth
   cp -r "$srcdir"/$_gitname/tests "$pkgdir"/usr/share/osgearth
   cp -r "$srcdir"/$_gitname/data "$pkgdir"/usr/share/osgearth
   
  install -Dm 644 ../CMakeModules/FindOsgEarth.cmake "$pkgdir/usr/share/cmake/Modules/FindOsgEarth.cmake"
  install -Dm 644 ../LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
