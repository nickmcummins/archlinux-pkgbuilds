# Maintainer: Nick M Cummins <nickmcummins at linuxmail dot org>

pkgname=3dforest-git
_pkgname=3dforest
pkgver=1.0.0
_pkgver=1.0.0
pkgrel=1
pkgdesc="Visualization, processing and analysis of Lidar point clouds, mainly focused on forest environment. New version of 3D Forest. Process files with terabytes of data."
arch=(x86_64)
url="https://www.3dforest.eu"
license=(GPL3)
depends=()
makedepends=(cmake)
provides=(3dforest)
conflicts=(3dforest)
source=('git+https://github.com/VUKOZ-OEL/3d-forest'
    '3DForest.png'
    '3DForest.desktop')
sha256sums=('SKIP'
    'SKIP'
    'SKIP')

pkgver() {
    cd ${srcdir}/3d-forest
    printf "${_pkgver}.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd ${srcdir}/3d-forest

    mkdir -p cmake-build-release && cd cmake-build-release

    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DOpenGL_GL_PREFERENCE=GLVND
    make
}

package() {
    cd ${srcdir}/3d-forest/cmake-build-release
	
    make DESTDIR="${pkgdir}" install

    install -D -m644 ${srcdir}/3DForest.desktop -t ${pkgdir}/usr/share/applications
}
