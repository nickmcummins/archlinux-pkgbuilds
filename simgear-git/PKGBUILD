# Maintainer: Andrew Whatson <whatson@gmail.com>
# Contributor: Glen D'souza <gdsouza@linuxmail.org>
# Contributor: jmf <jmf at mesecons dot net>
# Contributor: Pascal Groschwitz <p.groschwitz@googlemail.com>
# Contributor: Nick M Cummins <nickmcummins at linuxmail dot org>

pkgname=simgear-git
_pkgname=simgear
pkgver=2020.3.11r5587.a157e503
pkgrel=1
pkgdesc="A set of open-source libraries designed to be used as building blocks for quickly assembling 3d simulations, games, and visualization applications."
arch=('x86_64')
url="http://home.flightgear.org/"
license=('GPL')
depends=('glu' 'glut' 'freealut' 'plib' 'openscenegraph')
makedepends=('boost' 'cmake' 'git' 'mesa')
provides=("simgear=${pkgver}")
conflicts=('simgear')
options=('staticlibs')
source=("simgear::git+https://git.code.sf.net/p/flightgear/simgear#branch=release/2020.3" 'cmake-use-osgearth.patch')
md5sums=('SKIP' 'SKIP')

pkgver() {
  cd ${srcdir}/${_pkgname}
  printf "%sr%s.%s" \
    "$(tr -d '\n' < simgear-version)" \
    "$(git rev-list --count HEAD)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd ${srcdir}/${_pkgname}
  patch -Np0 -i ${srcdir}/cmake-use-osgearth.patch  

  mkdir -p cmake-build-release && cd cmake-build-release
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_MODULE_PATH=/usr/share/cmake/Modules \
    -DENABLE_TESTS=off
  make -j8
}

package() {
  cd ${srcdir}/${_pkgname}/cmake-build-release
  make DESTDIR="$pkgdir" install
}
