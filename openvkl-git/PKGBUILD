# Maintainer: Nick M Cummins <nickmcummins at linuxmail dot org>

pkgname=openvkl-git
_pkgname=openvkl
pkgver=0.10.0.r882.1f8724d
pkgrel=1
pkgdesc="Intel(R) Open Volume Kernel Library. A collection of high-performance volume computation kernels, developed at Intel."
arch=(x86_64)
url="https://www.openvkl.org/"
license=(Apache)
depends=(embree)
makedepends=(cmake)
provides=(openvkl)
conflicts=(openvkl)
source=("git+https://github.com/openvkl/openvkl")
sha256sums=('SKIP')

pkgver() {
    cd ${srcdir}/${_pkgname}
    VERSION=$(python -c "cmakelists = open('CMakeLists.txt', 'r'); cmakelists_lines = cmakelists.read().split('\n'); projectdef = list(filter(lambda line: line.startswith('project'), cmakelists_lines))[0].split(' '); print(projectdef[projectdef.index('VERSION') + 1])")
    printf "$VERSION.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd ${srcdir}/${_pkgname}

    mkdir -p build && cd build

    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
    make
}

package() {
    cd ${srcdir}/${_pkgname}/build
    make DESTDIR="${pkgdir}" install
}
