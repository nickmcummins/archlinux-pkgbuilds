# Maintainer: Nick M Cummins <nickmcummins at linuxmail dot org>

pkgname=ospray-git
_pkgname=ospray
pkgver=2.4.0.r8616.616dcea05
_cmake_version_file="cmake/ospray_version.cmake"
pkgrel=1
pkgdesc="A Ray Tracing Based Rendering Engine for High-Fidelity Visualization (git version)"
arch=(x86_64)
url="https://www.ospray.org/"
license=(Apache)
depends=(intel-tbb embree ispc libgl glfw glu openmpi rkcommon ispc110-bin openvkl)
makedepends=(cmake)
provides=(ospray=$pkgver)
conflicts=(ospray)
source=("git+https://github.com/ospray/ospray")
sha256sums=('SKIP')

pkgver() {
    cd ${srcdir}/${_pkgname}
    VERSION=$(python3 -c "import sys; import re; cmake_filename = sys.argv[1]; cmake_file = open(cmake_filename, 'r'); setvar_regex = r'(set\s*\()([^\s]+)\s([^)]+)'; vars = dict(list(map(lambda varline: (re.search(setvar_regex, varline).group(2), re.search(setvar_regex, varline).group(3)), filter(lambda line: line.startswith('set') and line.endswith(')'), cmake_file.read().split('\n'))))); cmake_package = list(filter(lambda var: var.endswith('_VERSION_MAJOR'), vars.keys()))[0].split('_')[0]; version_major = vars[f'{cmake_package}_VERSION_MAJOR']; version_minor = vars[f'{cmake_package}_VERSION_MINOR']; version_patch = vars[f'{cmake_package}_VERSION_PATCH']; print(f'{version_major}.{version_minor}.{version_patch}')
" ${_cmake_version_file})
    printf "$VERSION.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd ${srcdir}/${_pkgname}

    mkdir -p build && cd build

    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DISPC_EXECUTABLE=/opt/ispc-1.10.0/bin/ispc \
        -DISPC_VERSION=1.10.0 \
        -DOSPRAY_BUILD_ISA=ALL \
        -DOSPRAY_ENABLE_TESTING=ON \
        -DOSPRAY_MODULE_MPI=ON
    make
}

package() {
    cd ${srcdir}/${_pkgname}/build
    make DESTDIR="${pkgdir}" install
}
