# Original PKGBUILD Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Nick M Cummins <nickmcummins.at.linuxmail.dot.org>

pkgname=soplex-git
_pkgname=soplex
pkgver=8.0.0.r5564.85549f2c
_pkgver=8.0.0
pkgrel=1
pkgdesc='Sequential object-oriented simPlex (git version)'
arch=(x86_64)
url='https://www.scipopt.org/'
license=('Apache-2.0')
depends=(gcc-libs
         glibc
         gmp
         mpfr
         zlib)
makedepends=('boost' 'cmake' 'git')
provides=('soplex')
conflicts=('soplex')
options=(!lto)
source=('git+https://github.com/scipopt/soplex')
sha256sums=('SKIP')

pkgver() {
  cd ${srcdir}/${_pkgname}
  printf "${_pkgver}.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${srcdir}/${_pkgname}
  mkdir -p cmake-build-release && cd cmake-build-release

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DMPFR=ON

  make
}

package() {
  cd ${srcdir}/${_pkgname}/cmake-build-release
  make DESTDIR="$pkgdir" install
}
