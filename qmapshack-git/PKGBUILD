# Maintainer: Nick M Cummins <nickmcummins at linuxmail dot org>

pkgname=qmapshack-git
pkgver=1.19.0.r5115.ed9f8ccb
pkgrel=1
pkgdesc='Plan your next outdoor trip'
arch=('x86_64')
url='https://bitbucket.org/maproom/qmapshack (git version)'
license=('GPL3')
depends=('gdal' 'desktop-file-utils' 'quazip' 'qt6-webengine' 'routino>=3.1' 'alglib' 'quazip-qt6' 'qt6-tools-desktop')
makedepends=('cmake' 'python')
conflicts=('qmapshack' 'qmapshack-hg')
provides=('qmapshack')
source=("$pkgname::git+https://github.com/Maproom/qmapshack#branch=dev")
sha256sums=('SKIP')

pkgver() {
	cd ${srcdir}/${pkgname}
	# Use the tag of the last commit
	REVISION=$(printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)")
	GITVERSION=$(git describe --long --tags | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g')
	VERSION=$(python -c "import sys; print(sys.argv[1].split('.r')[0].replace('V_', '').strip())" ${GITVERSION})
	printf "${VERSION}.${REVISION}"
}

build() {
    cd ${srcdir}/${pkgname}

    mkdir -p cmake-build-release && cd cmake-build-release

    jobs=$(if ${ARCHLINUX_MAKEPKG_MAKE_JOBS+"false"}; then echo 1; else echo $ARCHLINUX_MAKEPKG_MAKE_JOBS; fi)

    cmake .. \
     -DCMAKE_INSTALL_PREFIX=/usr \
     -DCMAKE_BUILD_TYPE=Release \
     -DCMAKE_POLICY_VERSION_MINIMUM=3.5
    make -j$jobs
}

package() {
    cd ${srcdir}/${pkgname}/cmake-build-release
    make DESTDIR="${pkgdir}" install
}
