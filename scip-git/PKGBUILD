# Maintainer: Nick M Cummins <nickmcummins.at.linuxmail.dot.org>

pkgname=scip-git
_pkgname=scip
pkgver=10.0.0.r45629.ec8ae324dd
_pkgver=10.0.0
pkgrel=1
pkgdesc='Solving Constraint Integer Programs - solvers for mixed integer programming (MIP) and mixed integer nonlinear programming (MINLP) (git version)'
arch=(x86_64)
url='https://www.scipopt.org/'
license=('Apache-2.0')
depends=('papilo' 'zimpl')
makedepends=('boost' 'cmake' 'git')
provides=('scip')
conflicts=('scip')
options=(!lto)
source=('git+https://github.com/scipopt/scip')
sha256sums=('SKIP')

pkgver() {
  cd ${srcdir}/${_pkgname}
  printf "${_pkgver}.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${srcdir}/${_pkgname}
  mkdir -p cmake-build-release && cd cmake-build-release

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DMPFR=ON

  make
}

package() {
  cd ${srcdir}/${_pkgname}/cmake-build-release
  make DESTDIR="$pkgdir" install
}
