pkgname=saga-gis-git
_pkgname=saga-gis
pkgver=9.11.0.r6863.e5c3b959c
pkgrel=1
pkgdesc="A Geographic Information System (GIS) software with immense capabilities for geodata processing and analysis. (git version)"
url="http://www.saga-gis.org"
license=("GPL3")
arch=('i686' 'x86_64')
depends=('wxgtk'
         'proj' 'lua-hpdf'
         'gdal'
         'libtiff'
         'unixodbc'
         'jasper'
         'swig'
         'opencv2'
         'kealib'
         'pdal')
optdepends=('opencv'
            'vigra'
            'liblas'
            'libharu')
source=("${_pkgname}::git+git://git.code.sf.net/p/saga-gis/code")
md5sums=('SKIP')
provides=('saga-gis')
conflicts=('saga-gis')
pkgver() {
  cd ${srcdir}/${_pkgname}/${_pkgname}
  cmake_version=$(python ${srcdir}/../../read_cmake_version.py version.cmake SAGA_VERSION_MAJOR SAGA_VERSION_MINOR SAGA_VERSION_PATCH)
  printf "${cmake_version}.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${srcdir}/${_pkgname}/${_pkgname}

  mkdir -p cmake-build-release && cd cmake-build-release

  jobs=$(if ${ARCHLINUX_MAKEPKG_MAKE_JOBS+"false"}; then echo 1; else echo $ARCHLINUX_MAKEPKG_MAKE_JOBS; fi)

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    -DWITH_GUI=ON \
    -DWITH_TOOLS=ON
  make -j$jobs
}

package () {
  cd ${srcdir}/${_pkgname}/${_pkgname}/cmake-build-release
  
  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/${_pkgname}/${_pkgname}/src/lgpl.txt" "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
  
  install -D -m 644 ${srcdir}/${_pkgname}/${_pkgname}/src/saga_core/saga_gui/res/*.desktop -t ${pkgdir}/usr/share/applications/

  install -Dm644 "${srcdir}/${_pkgname}/${_pkgname}/src/saga_core/saga_gui/res/saga.png" \
                   "${pkgdir}/usr/share/pixmaps/${_pkgname}.png"
}
