# Maintainer: Nick M Cummins <nickmcummins@linuxmail.org>
pkgname=city3d-git
_pkgname=city3d
pkgver=r188.c9299ef
pkgrel=1
pkgdesc="Large-scale LoD2 Building Reconstruction from Airborne LiDAR Point Clouds (git version)"
arch=(x86_64)
license=(GPL)
url="https://github.com/tudelft3d/city3d"
provides=('city3d')
conflicts=('city3d')
depends=()
source=("${_pkgname}::git+https://github.com/tudelft3d/City3D" 'cmake-add-install-target.patch' 'City3D.desktop')
sha256sums=('SKIP' 'SKIP' 'SKIP')

pkgver() {
  cd ${srcdir}/${_pkgname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${srcdir}/${_pkgname}
  patch -Np0 -i ${srcdir}/cmake-add-install-target.patch

  mkdir -p cmake-build-release && cd cmake-build-release

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
    -DCMAKE_CXX_FLAGS=-Wno-template-body

  make
}

package() {
  cd ${srcdir}/${_pkgname}/cmake-build-release
  make DESTDIR=${pkgdir} install

  mkdir -p ${pkgdir}/opt/City3D
  cp -a ${srcdir}/${_pkgname}/data ${pkgdir}/opt/City3D

  cd ${srcdir}/${_pkgname}/code/City3D/Resources
  icns2png -x City3D.icns

  for size in 16 32 64 128 256; do
      convert City3D_512x512x32.png -adaptive-resize ${size}x${size} ${srcdir}/City3D_${size}x${size}.png
      install -D -m 644 ${srcdir}/City3D_${size}x${size}.png ${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/City3D.png
  done

  install -D -m644 ${srcdir}/City3D.desktop -t ${pkgdir}/usr/share/applications
}
