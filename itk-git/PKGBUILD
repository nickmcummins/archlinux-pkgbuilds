# Maintainer: Nick M Cummins <nickmcummins at linuxmail dot org>

pkgname=itk-git
_pkgname=itk
pkgver=5.1.0.131.ged38c21c46
pkgrel=1
pkgdesc='Open-source, cross-platform C++ toolkit for segmentation and registration'
arch=('i686' 'x86_64')
url='http://www.itk.org'
license=('Apache')
depends=('hdf5-cpp-fortran' 'gdcm-git' 'libjpeg-turbo' 'libpng' 'libtiff')
makedepends=('git' 'cmake')
provides=("${_pkgname}" "insight-toolkit"{,-git})
conflicts=("${_pkgname}" "insight-toolkit"{,-git})
source=("${_pkgname}::git://git.code.sf.net/p/itk/code")
sha256sums=("SKIP")

pkgver() {
  cd ${_pkgname}
  git describe --always | sed 's:^v::;s:-:.:g'
}

build() {
  cd ${srcdir}/${_pkgname}
  mkdir -p build && cd build

  cmake $srcdir/${_pkgname} \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DBUILD_SHARED_LIBS=ON \
      -DBUILD_TESTING=OFF \
      -DITK_USE_SYSTEM_LIBRARIES=ON \
      -DITK_USE_SYSTEM_GDCM=ON \
      -DITK_WRAP_JAVA=OFF \
      -DITK_WRAP_PERL=OFF \
      -DITK_WRAP_PYTHON=OFF \
      -DITK_WRAP_RUBY=OFF \
      -DITK_WRAP_TCL=OFF \
      -DITKV4_COMPATIBILITY=ON \
      -DModule_ITKDeprecated=ON \
      -DModule_NeuralNetworks=ON
  make
}

package() {
  cd ${srcdir}/${_pkgname}/build
  make DESTDIR="$pkgdir" install
}
