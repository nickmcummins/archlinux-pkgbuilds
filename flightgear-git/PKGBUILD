# Maintainer: Andrew Whatson <whatson@gmail.com>
# Contributor: Glen D'souza <gdsouza@linuxmail.org>
# Contributor: jmf <jmf at mesecons dot net>
# Contributor: Pascal Groschwitz <p.groschwitz@googlemail.com>
# Contributor: Nick M Cummins <nickmcummins@linuxmail.org>

pkgname=flightgear-git
_pkgname=flightgear
pkgver=2024.2.0.r16600.32a34b5e8
pkgrel=1
pkgdesc='An open-source, multi-platform flight simulator (git version)'
arch=('x86_64')
url="https://home.flightgear.org"
license=('GPL')
depends=('libxmu' 'libxi' 'zlib' 'openscenegraph' 'libxrandr' 'glu' 'openal' 'osgxr' 'simgear')
makedepends=('boost' 'cmake' 'git' 'mesa' 'sharutils' 'qt5-base' 'qt5-declarative' 'qt5-svg')
optdepends=('qt5-base: fgfs --launcher'
            'qt5-declarative: fgfs --launcher'
            'flightgear-data')
provides=("flightgear=${pkgver}")
conflicts=('flightgear')
source=("flightgear::git+https://git.code.sf.net/p/flightgear/flightgear#branch=next" 'env-LD_LIBRARY_PATH-use-osg-flightgear-location-for-desktop-launcher.patch')
sha256sums=('SKIP' 'SKIP')

pkgver() {
  cd ${srcdir}/${_pkgname}
  printf "%s.r%s.%s" "$(tr -d '\n' < flightgear-version)" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${srcdir}/${_pkgname}

  patch -Np0 -i ${srcdir}/env-LD_LIBRARY_PATH-use-osg-flightgear-location-for-desktop-launcher.patch

  jobs=$(if ${ARCHLINUX_MAKEPKG_MAKE_JOBS+"false"}; then echo 1; else echo $ARCHLINUX_MAKEPKG_MAKE_JOBS; fi)

  mkdir -p cmake-build-release && cd cmake-build-release
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DFG_DATA_DIR:STRING="/usr/share/flightgear/data" \
    -DCMAKE_BUILD_TYPE=Release \
    -DFG_BUILD_TYPE=Release \
    -DOSG_INCLUDE_DIR=/opt/openscenegraph-flightgear/include \
    -DOSG_LIBRARY_RELEASE=/opt/openscenegraph-flightgear/lib/libosg.so \
    -DOSGDB_INCLUDE_DIR=/opt/openscenegraph-flightgear/include \
    -DOSGDB_LIBRARY_RELEASE=/opt/openscenegraph-flightgear/lib/libosgDB.so \
    -DOSGGA_INCLUDE_DIR=/opt/openscenegraph-flightgear/include \
    -DOSGGA_LIBRARY_RELEASE=/opt/openscenegraph-flightgear/lib/libosgGA.so \
    -DOSGSIM_INCLUDE_DIR=/opt/openscenegraph-flightgear/include \
    -DOSGSIM_LIBRARY_RELEASE=/opt/openscenegraph-flightgear/lib/libosgSim.so \
    -DOSGPARTICLE_INCLUDE_DIR=/opt/openscenegraph-flightgear/include \
    -DOSGPARTICLE_LIBRARY_RELEASE=/opt/openscenegraph-flightgear/lib/libosgParticle.so \
    -DOSGTERRAIN_INCLUDE_DIR=/opt/openscenegraph-flightgear/include \
    -DOSGTERRAIN_LIBRARY_RELEASE=/opt/openscenegraph-flightgear/lib/libosgTerrain.so \
    -DOSGTEXT_INCLUDE_DIR=/opt/openscenegraph-flightgear/include \
    -DOSGTEXT_LIBRARY_RELEASE=/opt/openscenegraph-flightgear/lib/libosgText.so \
    -DOSGUTIL_INCLUDE_DIR=/opt/openscenegraph-flightgear/include \
    -DOSGUTIL_LIBRARY_RELEASE=/opt/openscenegraph-flightgear/lib/libosgUtil.so \
    -DOSGVIEWER_INCLUDE_DIR=/opt/openscenegraph-flightgear/include \
    -DOSGVIEWER_LIBRARY_RELEASE=/opt/openscenegraph-flightgear/lib/libosgViewer.so \
    -DOPENTHREADS_LIBRARY_RELEASE=/opt/openscenegraph-flightgear/lib/libOpenThreads.so \
    -DosgPluginLocation=/opt/openscenegraph-flightgear/lib/osgPlugins-3.6.5
  make -j$jobs
}

package() {
  cd ${srcdir}/${_pkgname}/cmake-build-release
  make DESTDIR="$pkgdir" install

  cd ${srcdir}/flightgear
  install -Dm0644 package/flightgear.ico "$pkgdir"/usr/share/icons/flightgear.ico
  install -Dm0644 scripts/completion/fg-completion.bash "$pkgdir"/usr/share/bash-completion/completions/fgfs
  install -Dm0644 scripts/completion/fg-completion.zsh "$pkgdir"/usr/share/zsh/site-functions/_fgfs
  ln -sf flightgear "$pkgdir"/usr/share/FlightGear
}

