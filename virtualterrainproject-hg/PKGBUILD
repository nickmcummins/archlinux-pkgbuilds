# Maintainer: Nick Cummins <nickmcummins@linuxmail.org>
# Built: 2020-05-07 5:13PM
pkgname='virtualterrainproject-hg'
_pkgname='VirtualTerrainProject'
pkgver=1.5r130211
pkgrel=1
pkgdesc="The VTP has a suite of cross-platform applications and libraries for geospatial processing and 3D visualization of terrain."
arch=('i686' 'x86_64')
url='http://vterrain.org/'
license=('MIT')
provides=('virtualterrainproject')
depends=('libmini' 'libgrid-svn' 'openscenegraph' 'glu' 'quikgrid' 'libmini')
makedepends=('gdal' 'openscenegraph' 'libmini' 'cmake' 'mercurial' 'inkscape')
optdepends=('virtualterrainproject-data')
options=()
source=("$pkgname::hg+https://bitbucket.org/bdiscoe/vtp"
    "vtp-src-fixes.patch"
    "VTBuilder.desktop"
    "Enviro.desktop")
md5sums=('SKIP'
    'SKIP'
    'SKIP'
    'SKIP')

build() {
  cd "${pkgname}"  
  
  patch -Np0 -i "${srcdir}"/vtp-src-fixes.patch
 
  inkscape --export-plain-svg=${srcdir}/VTBuilder.svg $srcdir/$pkgname/TerrainApps/VTBuilder/icons/VTBuilder.ico
  inkscape --export-plain-svg=$srcdir/Enviro.svg $srcdir/$pkgname/TerrainApps/Enviro/osx/Enviro.icns
  
  mkdir -p build
  cd build
  export CPATH=/usr/include/qt
  cmake ../ \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    -DOpenGL_GL_PREFERENCE=LEGACY \
    -DMINI_INCLUDE_DIR=/usr/include \
    -DMINI_LIBRARY=/usr/lib/libMini.so \
    -DVTP_USE_CPACK=OFF \
    -DQUIKGRID_INCLUDE_DIR=/usr/include \
    -DQUIKGRID_LIBRARY=/usr/lib/libquikgrid.so
  make
}

package() {
  cd ${pkgname}/build

  make DESTDIR=${pkgdir} install || return 1
  install -D -m644 ${srcdir}/*.desktop -t ${pkgdir}/usr/share/applications
  
  install -D -m644 "${srcdir}/${pkgname}/license.txt" "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
  install -D -m644 "${srcdir}/VTBuilder.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/VTBuilder.svg"
  install -D -m644 "${srcdir}/Enviro.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/Enviro.svg"
  
  mkdir -p "${pkgdir}/opt/${_pkgname}"
  mkdir -p "${pkgdir}/opt/${_pkgname}/Apps"
  
  cd "${srcdir}"/"${pkgname}"
  cp -a Docs "${pkgdir}/opt/${_pkgname}"
  cp -a Installers/*.xml "${pkgdir}/opt/${_pkgname}/Apps"
}
